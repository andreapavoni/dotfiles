# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# All of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# Load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

# Load every completion after autocomplete loads
autoload -U compinit && compinit
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

# Autoload all user-defined functions
for zdir in $( find $ZSH -type d -name zfunctions ); do
  fpath=($zdir $fpath)
  autoload $zdir/*(:t)
done

unset config_files
